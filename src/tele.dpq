module Tele where
import "lib/Prelude.dpq"


bell00 : !(Unit -> Qubit * Qubit)
bell00 u =
  let a = Init0 ()
      b = Init0 ()
  in CNot b (H a)

alice1 : !(Qubit -> Qubit -> Bit * Bit)
alice1 a q =
  let (a, q) = CNot a q
      q = H q
  in (Meas a, Meas q)

bob1 : !(Qubit -> Bit -> Bit -> Qubit)
bob1 q x y =
  let (q, x) = C_X q x
      (q, y) = C_Z q y
      _ = Discard x
      _ = Discard y
  in q

tele1 : !(Qubit -> Qubit)
tele1 q =
  let (b, a) = bell00 ()
      (x, y) = alice1 a q
      z = bob1 b x y
  in z

boxTele : Circ(Qubit , Qubit)
boxTele = box Qubit tele1


alice2 : ! (Qubit -> Qubit -> Bool * Bool)
alice2 a q =
  let (a, q) = CNot a q
      q = H q
  in (dynlift (Meas a), dynlift (Meas q))


bob2 : ! (Qubit -> Bool -> Bool -> Qubit)
bob2 q x y =
  let q = if x then QNot q else q
      q = if y then ZGate q else q
   in q

tele2 : ! (Qubit -> Qubit)
tele2 q =
  let (b, a) = bell00 ()
      (x, y) = alice2 a q
      z = bob2 b x y
  in z

tele_n_1 : ! forall (n : Nat) -> Vec Qubit n -> Vec Qubit n
tele_n_1 qs =
  case qs of
    VNil -> VNil
    VCons q qs' ->
      let q' = tele1 q
          qs'' = tele_n_1 qs'
      in (VCons q' qs'')

tele_n_2 : ! forall (n : Nat) -> Vec Qubit n -> Vec Qubit n
tele_n_2 qs =
  case qs of
    VNil -> VNil
    VCons q qs' ->
      let q' = tele2 q
          qs'' = tele_n_2 qs'
      in (VCons q' qs'')
